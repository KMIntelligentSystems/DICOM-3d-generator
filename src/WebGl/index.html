<!doctype html>
<html lang="en">
<head>
  <meta charset="utf-8">
  <title>LoaderApp</title>
  <base href="/">

  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="icon" type="image/x-icon" href="favicon.ico">

 <!-- <script type="text/javascript" src="dat.gui.min.js"></script>-->
  
  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r81/three.js"></script>
  <!--<script src="https://cdnjs.cloudflare.com/ajax/libs/ami.js/0.0.12/ami.min.js"></script>-->
 <!-- <script src="./../loader-app/node_modules/ami.js"></script>-->
  
</head>
<body>
  <!--<canvas id="myCanvas" width="384" height="512"></canvas>-->
  <app-root>
    
  </app-root>
  
</body>
    <!-- WebGL 2 shaders -->
    <script id="volume-vs" type="x-shader/x-vertex">
        precision highp float;
        attribute vec3 aVertexPosition;
        void main(void)
        {
            gl_Position = vec4(aVertexPosition, 1.0);
        }

        </script>
        
        <script id="volume-fs" type="x-shader/x-fragment">
            precision highp float;

            //Defined dynamically before compile...
            const vec2 slices = vec2(16.0,16.0);
            const int maxSamples = 256;
            
            uniform sampler2D uVolume;
            uniform sampler2D uTransferFunction;
            
            uniform vec3 uBBMin;
            uniform vec3 uBBMax;
            uniform vec3 uResolution;
            
            uniform bool uEnableColour;
            
            uniform float uBrightness;
            uniform float uContrast;
            uniform float uSaturation;
            uniform float uPower;
            
            uniform mat4 uPMatrix;
            uniform mat4 uInvPMatrix;
            uniform mat4 uMVMatrix;
            uniform mat4 uNMatrix;
            uniform vec4 uViewport;
            uniform int uSamples;
            uniform float uDensityFactor;
            uniform float uIsoValue;
            uniform vec4 uIsoColour;
            uniform float uIsoSmooth;
            uniform int uIsoWalls;
            uniform int uFilter;
            uniform vec2 uRange;
            uniform vec2 uDenMinMax;
            
            //#define tex3D(pos) interpolate_tricubic_fast(pos)
            //#define tex3D(pos) texture3Dfrom2D(pos).x
            
            vec2 islices = vec2(1.0 / slices.x, 1.0 / slices.y);
            
            vec4 texture3Dfrom2D(vec3 pos)
            {
              //Get z slice index and position between two slices
              float Z = pos.z * slices.x * slices.y;
              int slice = int(Z); //Index of first slice
            
              //X & Y coords of sample scaled to slice size
              vec2 sampleOffset = pos.xy * islices;
              //Offsets in 2D texture of given slice indices
              //(add offsets to scaled position within slice to get sample positions)
              float A = float(slice) * islices.x;
              float B = float(slice+1) * islices.x;
              vec2 z1offset = vec2(fract(A), floor(A) / slices.y) + sampleOffset;
              vec2 z2offset = vec2(fract(B), floor(B) / slices.y) + sampleOffset;
              
              //Interpolate the final value by position between slices [0,1]
              return mix(texture2D(uVolume, z1offset), texture2D(uVolume, z2offset), fract(Z));
            }
            
            float interpolate_tricubic_fast(vec3 coord);
            
            float tex3D(vec3 pos) 
            {
              if (uFilter > 0)
                return interpolate_tricubic_fast(pos);
              return texture3Dfrom2D(pos).x;
            }
            
            // It seems WebGL has no transpose
            mat4 transpose(in mat4 m)
            {
              return mat4(
                          vec4(m[0].x, m[1].x, m[2].x, m[3].x),
                          vec4(m[0].y, m[1].y, m[2].y, m[3].y),
                          vec4(m[0].z, m[1].z, m[2].z, m[3].z),
                          vec4(m[0].w, m[1].w, m[2].w, m[3].w)
                         );
            }
            
            //Light moves with camera
            const vec3 lightPos = vec3(0.5, 0.5, 5.0);
            const float ambient = 0.2;
            const float diffuse = 0.8;
            const vec3 diffColour = vec3(1.0, 1.0, 1.0);  //Colour of diffuse light
            const vec3 ambColour = vec3(0.2, 0.2, 0.2);   //Colour of ambient light
            
            void lighting(in vec3 pos, in vec3 normal, inout vec3 colour)
            {
              vec4 vertPos = uMVMatrix * vec4(pos, 1.0);
              vec3 lightDir = normalize(lightPos - vertPos.xyz);
              vec3 lightWeighting = ambColour + diffColour * diffuse * clamp(abs(dot(normal, lightDir)), 0.1, 1.0);
            
              colour *= lightWeighting;
            }
            
            vec3 isoNormal(in vec3 pos, in vec3 shift, in float density)
            {
              vec3 shiftpos = vec3(pos.x + shift.x, pos.y + shift.y, pos.z + shift.z);
              vec3 shiftx = vec3(shiftpos.x, pos.y, pos.z);
              vec3 shifty = vec3(pos.x, shiftpos.y, pos.z);
              vec3 shiftz = vec3(pos.x, pos.y, shiftpos.z);
            
              //Detect bounding box hit (walls)
              if (uIsoWalls > 0)
              {
                if (pos.x <= uBBMin.x) return vec3(-1.0, 0.0, 0.0);
                if (pos.x >= uBBMax.x) return vec3(1.0, 0.0, 0.0);
                if (pos.y <= uBBMin.y) return vec3(0.0, -1.0, 0.0);
                if (pos.y >= uBBMax.y) return vec3(0.0, 1.0, 0.0);
                if (pos.z <= uBBMin.z) return vec3(0.0, 0.0, -1.0);
                if (pos.z >= uBBMax.z) return vec3(0.0, 0.0, 1.0);
              }
            
              //Calculate normal
              return vec3(density) - vec3(tex3D(shiftx), tex3D(shifty), tex3D(shiftz));
            }
            
            vec2 rayIntersectBox(vec3 rayDirection, vec3 rayOrigin)
            {
              //Intersect ray with bounding box
              vec3 rayInvDirection = 1.0 / rayDirection;
              vec3 bbMinDiff = (uBBMin - rayOrigin) * rayInvDirection;
              vec3 bbMaxDiff = (uBBMax - rayOrigin) * rayInvDirection;
              vec3 imax = max(bbMaxDiff, bbMinDiff);
              vec3 imin = min(bbMaxDiff, bbMinDiff);
              float back = min(imax.x, min(imax.y, imax.z));
              float front = max(max(imin.x, 0.0), max(imin.y, imin.z));
              return vec2(back, front);
            }
            
            void main()
            {
                //Compute eye space coord from window space to get the ray direction
                mat4 invMVMatrix = transpose(uMVMatrix);
                //ObjectSpace *[MV] = EyeSpace *[P] = Clip /w = Normalised device coords ->VP-> Window
                //Window ->[VP^]-> NDC ->[/w]-> Clip ->[P^]-> EyeSpace ->[MV^]-> ObjectSpace
                vec4 ndcPos;
                ndcPos.xy = ((2.0 * gl_FragCoord.xy) - (2.0 * uViewport.xy)) / (uViewport.zw) - 1.0;
                ndcPos.z = (2.0 * gl_FragCoord.z - gl_DepthRange.near - gl_DepthRange.far) /
                           (gl_DepthRange.far - gl_DepthRange.near);
                ndcPos.w = 1.0;
                vec4 clipPos = ndcPos / gl_FragCoord.w;
                //vec4 eyeSpacePos = uInvPMatrix * clipPos;
                vec3 rayDirection = normalize((invMVMatrix * uInvPMatrix * clipPos).xyz);
            
                //Ray origin from the camera position
                vec4 camPos = -vec4(uMVMatrix[3]);  //4th column of modelview
                vec3 rayOrigin = (invMVMatrix * camPos).xyz;
            
                //Calc step
                float stepSize = 1.732 / float(uSamples); //diagonal of [0,1] normalised coord cube = sqrt(3)
            
                //Intersect ray with bounding box
                vec2 intersection = rayIntersectBox(rayDirection, rayOrigin);
                //Subtract small increment to avoid errors on front boundary
                intersection.y -= 0.000001;
                //Discard points outside the box (no intersection)
                if (intersection.x <= intersection.y) discard;
            
                vec3 rayStart = rayOrigin + rayDirection * intersection.y;
                vec3 rayStop = rayOrigin + rayDirection * intersection.x;
            
                vec3 step = normalize(rayStop-rayStart) * stepSize;
                vec3 pos = rayStart;
            
                float T = 1.0;
                vec3 colour = vec3(0.0);
                bool inside = false;
                vec3 shift = uIsoSmooth / uResolution;
                //Number of samples to take along this ray before we pass out back of volume...
                float travel = distance(rayStop, rayStart) / stepSize;
                int samples = int(ceil(travel));
                float range = uRange.y - uRange.x;
                if (range <= 0.0) range = 1.0;
                //Scale isoValue
                float isoValue = uRange.x + uIsoValue * range;
              
                //Raymarch, front to back
                for (int i=0; i < maxSamples; ++i)
                {
                  //Render samples until we pass out back of cube or fully opaque
            #ifndef IE11
                  if (i == samples || T < 0.01) break;
            #else
                  //This is slower but allows IE 11 to render, break on non-uniform condition causes it to fail
                  if (i == uSamples) break;
                  if (all(greaterThanEqual(pos, uBBMin)) && all(lessThanEqual(pos, uBBMax)))
            #endif
                  {
                    //Get density 
                    float density = tex3D(pos);
            
            #define ISOSURFACE
            #ifdef ISOSURFACE
                    //Passed through isosurface?
                    if (isoValue > uRange.x && ((!inside && density >= isoValue) || (inside && density < isoValue)))
                    {
                      inside = !inside;
                      //Find closer to exact position by iteration
                      //http://sizecoding.blogspot.com.au/2008/08/isosurfaces-in-glsl.html
                      float exact;
                      float a = intersection.y + (float(i)*stepSize);
                      float b = a - stepSize;
                      for (int j = 0; j < 5; j++)
                      {
                        exact = (b + a) * 0.5;
                        pos = rayDirection * exact + rayOrigin;
                        density = tex3D(pos);
                        if (density - isoValue < 0.0)
                          b = exact;
                        else
                          a = exact;
                      }
            
                      //Skip edges unless flagged to draw
                      if (uIsoWalls > 0 || all(greaterThanEqual(pos, uBBMin)) && all(lessThanEqual(pos, uBBMax)))
                      {
                        vec4 value = vec4(uIsoColour.rgb, 1.0);
            
                        //normal = normalize(normal);
                        //if (length(normal) < 1.0) normal = vec3(0.0, 1.0, 0.0);
                        vec3 normal = normalize(mat3(uNMatrix) * isoNormal(pos, shift, density));
            
                        vec3 light = value.rgb;
                        lighting(pos, normal, light);
                        //Front-to-back blend equation
                        colour += T * uIsoColour.a * light;
                        T *= (1.0 - uIsoColour.a);
                      }
                    }
            #endif
            
                    if (uDensityFactor > 0.0)
                    {
                      //Normalise the density over provided range
                      density = (density - uRange.x) / range;
                      density = clamp(density, 0.0, 1.0);
                      if (density < uDenMinMax[0] || density > uDenMinMax[1])
                      {
                        //Skip to next sample...
                        pos += step;
                        continue;
                      }
            
                      density = pow(density, uPower); //Apply power
            
                      vec4 value;
                      if (uEnableColour)
                        value = texture2D(uTransferFunction, vec2(density, 0.5));
                      else
                        value = vec4(density);
            
                      value *= uDensityFactor * stepSize;
            
                      //Color
                      colour += T * value.rgb;
                      //Alpha
                      T *= 1.0 - value.a;
                    }
                  }
                  
                  //Next sample...
                  pos += step;
                }
            
                //Apply brightness, saturation & contrast
                colour += uBrightness;
                const vec3 LumCoeff = vec3(0.2125, 0.7154, 0.0721);
                vec3 AvgLumin = vec3(0.5, 0.5, 0.5);
                vec3 intensity = vec3(dot(colour, LumCoeff));
                colour = mix(intensity, colour, uSaturation);
                colour = mix(AvgLumin, colour, uContrast);
            
                if (T > 0.95) discard;
                gl_FragColor = vec4(colour, 1.0 - T);
            
            #ifdef WRITE_DEPTH
                /* Write the depth !Not supported in WebGL without extension */
                vec4 clip_space_pos = uPMatrix * uMVMatrix * vec4(rayStart, 1.0);
                float ndc_depth = clip_space_pos.z / clip_space_pos.w;
                float depth = (((gl_DepthRange.far - gl_DepthRange.near) * ndc_depth) + 
                                 gl_DepthRange.near + gl_DepthRange.far) / 2.0;
                gl_FragDepth = depth;
            #endif
            }
            
            float interpolate_tricubic_fast(vec3 coord)
            {
            /* License applicable to this function:
            Copyright (c) 2008-2013, Danny Ruijters. All rights reserved.
            Redistribution and use in source and binary forms, with or without
            modification, are permitted provided that the following conditions are met:
            *  Redistributions of source code must retain the above copyright
               notice, this list of conditions and the following disclaimer.
            *  Redistributions in binary form must reproduce the above copyright
               notice, this list of conditions and the following disclaimer in the
               documentation and/or other materials provided with the distribution.
            *  Neither the name of the copyright holders nor the names of its
               contributors may be used to endorse or promote products derived from
               this software without specific prior written permission.
            THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
            AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
            IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
            ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
            LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
            CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
            SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
            INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
            CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
            ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGE.
            The views and conclusions contained in the software and documentation are
            those of the authors and should not be interpreted as representing official
            policies, either expressed or implied.
            When using this code in a scientific project, please cite one or all of the
            following papers:
            *  Daniel Ruijters and Philippe Th√©venaz,
               GPU Prefilter for Accurate Cubic B-Spline Interpolation, 
               The Computer Journal, vol. 55, no. 1, pp. 15-20, January 2012.
            *  Daniel Ruijters, Bart M. ter Haar Romeny, and Paul Suetens,
               Efficient GPU-Based Texture Interpolation using Uniform B-Splines,
               Journal of Graphics Tools, vol. 13, no. 4, pp. 61-69, 2008.
            */
              // shift the coordinate from [0,1] to [-0.5, nrOfVoxels-0.5]
              vec3 nrOfVoxels = uResolution; //textureSize3D(tex, 0));
              vec3 coord_grid = coord * nrOfVoxels - 0.5;
              vec3 index = floor(coord_grid);
              vec3 fraction = coord_grid - index;
              vec3 one_frac = 1.0 - fraction;
            
              vec3 w0 = 1.0/6.0 * one_frac*one_frac*one_frac;
              vec3 w1 = 2.0/3.0 - 0.5 * fraction*fraction*(2.0-fraction);
              vec3 w2 = 2.0/3.0 - 0.5 * one_frac*one_frac*(2.0-one_frac);
              vec3 w3 = 1.0/6.0 * fraction*fraction*fraction;
            
              vec3 g0 = w0 + w1;
              vec3 g1 = w2 + w3;
              vec3 mult = 1.0 / nrOfVoxels;
              vec3 h0 = mult * ((w1 / g0) - 0.5 + index);  //h0 = w1/g0 - 1, move from [-0.5, nrOfVoxels-0.5] to [0,1]
              vec3 h1 = mult * ((w3 / g1) + 1.5 + index);  //h1 = w3/g1 + 1, move from [-0.5, nrOfVoxels-0.5] to [0,1]
            
              // fetch the eight linear interpolations
              // weighting and fetching is interleaved for performance and stability reasons
              float tex000 = texture3Dfrom2D(h0).r;
              float tex100 = texture3Dfrom2D(vec3(h1.x, h0.y, h0.z)).r;
              tex000 = mix(tex100, tex000, g0.x);  //weigh along the x-direction
              float tex010 = texture3Dfrom2D(vec3(h0.x, h1.y, h0.z)).r;
              float tex110 = texture3Dfrom2D(vec3(h1.x, h1.y, h0.z)).r;
              tex010 = mix(tex110, tex010, g0.x);  //weigh along the x-direction
              tex000 = mix(tex010, tex000, g0.y);  //weigh along the y-direction
              float tex001 = texture3Dfrom2D(vec3(h0.x, h0.y, h1.z)).r;
              float tex101 = texture3Dfrom2D(vec3(h1.x, h0.y, h1.z)).r;
              tex001 = mix(tex101, tex001, g0.x);  //weigh along the x-direction
              float tex011 = texture3Dfrom2D(vec3(h0.x, h1.y, h1.z)).r;
              float tex111 = texture3Dfrom2D(h1).r;
              tex011 = mix(tex111, tex011, g0.x);  //weigh along the x-direction
              tex001 = mix(tex011, tex001, g0.y);  //weigh along the y-direction
            
              return mix(tex001, tex000, g0.z);  //weigh along the z-direction
            }
        </script>

        <script id="texture-vs" type="x-shader/x-vertex">
        
        precision highp float;
        attribute vec3 aVertexPosition;
        uniform mat4 uMVMatrix;
        varying vec2 vCoord;
        void main(void) {
           gl_Position = vec4(aVertexPosition, 1.0);

            //Apply translation, rotation & scaling matrix to vertices to get coords
            vec4 coords = uMVMatrix * vec4(aVertexPosition.xy, 0.0, 1.0);
           vCoord = coords.xy; 
            
 

        }
        </script>
        
        <script id="texture-fs" type="x-shader/x-fragment">
            precision mediump float;
            #define rgba vec4
            
            //Palette lookup mu = [0,1]
            #define gradient(mu) texture2D(palette, vec2(mu, 0.0))
            
            //Uniform data
            uniform sampler2D palette;
            uniform sampler2D texture;
            
            uniform int colourmap;
            uniform float bright;
            uniform float cont;
            uniform float power;
            
            uniform int axis;
            uniform vec3 slice;
            uniform ivec3 res;
            uniform vec2 dim;
            
            uniform ivec2 select;
            
            //Current coordinate
            varying vec2 vCoord;
            
            void main()
            {
              bool invert = false;
              vec2 coord;
              float z;
            
              if (int(gl_FragCoord.x) == select.x) invert = true;
              if (int(gl_FragCoord.y) == select.y) invert = true;
            
              if (axis==0)
              {
                //x-axis slice
                //slice offset coords from vCoord.x, inside coords from (slice,vCoord.y)
                z = vCoord.x * float(res.z);
                coord = vec2(clamp(slice.x, 0.0, 0.999), vCoord.y);
              }
              else if (axis==1)
              {
                //y-axis slice
                //slice offset coords from vCoord.y, inside coords from (vCoord.x,slice)
                z = vCoord.y * float(res.z);
                coord = vec2(vCoord.x, clamp(slice.y, 0.0, 0.999));
              }
              else if (axis==2)
              {
                //z-axis slice
                //slice offset coords from slice.z, inside coords unchanged (vCoord.xy)
                z = slice.z * float(res.z);
                coord = vCoord;
              }
            
              //Get offsets to selected slice
              float xy = z/dim.x;
              int row = int(xy);
              //mod() function doesn't work properly on safari, use fract() instead
              //int col = int(fract(xy) * dim.x);
              int col = int(fract(xy) * dim.x);
              coord += vec2(float(col), float(row));
              //Rescale to texture coords [0,1]
              coord /= dim;
            
              //Get texture value at coord and calculate final colour
              vec4 tex = texture2D(texture, coord);
              float lum = tex.r; //0.3 * tex.r + 0.59 * tex.g + 0.11 * tex.b;
              lum = pow(lum, power);
              vec4 pixelColor;
              if (colourmap == 1)
              {
                pixelColor = gradient(lum);
              }
              else
              {
                pixelColor = vec4(lum, lum, lum, 1.0);
              }
              pixelColor.rgb = ((pixelColor.rgb - 0.5) * max(cont, 0.0)) + 0.5;
              pixelColor.rgb += bright;
              if (invert)
              {
                pixelColor.rgb = vec3(1.0) - pixelColor.rgb;
                pixelColor.a = 1.0;
              }
              gl_FragColor = pixelColor;
            }
        </script>
        
        <script id="line-vs" type="x-shader/x-vertex">
            precision highp float;

            attribute vec3 aVertexPosition;
            attribute vec4 aVertexColour;
            
            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            
            uniform vec4 uColour;
            uniform float uAlpha;
            
            varying vec4 vColour;
            
            void main(void)
            {
              vec4 mvPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
              gl_Position = uPMatrix * mvPosition;
              vec4 colour = aVertexColour;
              float alpha = 1.0;
              if (uColour.a > 0.01) colour = uColour;
              if (uAlpha > 0.01) alpha = uAlpha;
              vColour = vec4(colour.rgb, colour.a * alpha);
            }
        </script>
        
        <script id="line-fs" type="x-shader/x-fragment">
            precision highp float;
            varying vec4 vColour;
            
            void main(void)
            {
              gl_FragColor = vColour;
            }
        </script>

    <script id="cube-vertex-shader" type="x-shader/x-vertex">
        attribute vec2 attribute_vertex_position;
        uniform vec3 uniform_camera_up;
        uniform vec3 uniform_camera_right;
        uniform vec3 uniform_camera_dir;
        uniform float uniform_camera_near;
        varying vec3 varying_pixel_position;
        void main()
        {
            gl_Position = vec4(attribute_vertex_position, 0.0, 1.0);
            varying_pixel_position =
                attribute_vertex_position[0] * uniform_camera_right +
                attribute_vertex_position[1] * uniform_camera_up +
                uniform_camera_near * uniform_camera_dir;
        }
        </script>
        
        <script id="argb-srgb" type="x-shader/x-fragment">
        float srgb(float v)
        {
            v = clamp(v, 0.0, 1.0);
            float K0 = 0.03928;
            float a = 0.055;
            float phi = 12.92;
            float gamma = 2.4;
            return v <= K0 / phi ? v * phi : (1.0 + a) * pow(v, 1.0 / gamma) - a;
        }
        float linear(float v)
        {
            v = clamp(v, 0.0, 1.0);
            float K0 = 0.03928;
            float a = 0.055;
            float phi = 12.92;
            float gamma = 2.4;
            return v <= K0 ? v / phi : pow((v + a) / (1.0 + a), gamma);
        }
        vec4 argb(vec3 c)
        {
            return vec4(srgb(c.r), srgb(c.g), srgb(c.b), 1.0);
        }
        vec3 texture(samplerCube sampler, vec3 c)
        {
            vec3 s = textureCube(sampler, c).rgb;
            return vec3(linear(s.r), linear(s.g), linear(s.b));
        }
        </script>
        
        <script id="argb-linear" type="x-shader/x-fragment">
        vec4 argb(vec3 c)
        {
            return vec4(c, 1.0);
        }
        vec3 texture(samplerCube sampler, vec3 c)
        {
            return textureCube(sampler, c).rgb;
        }
        </script>
        
        <script id="cube-fragment-shader" type="x-shader/x-fragment">
        varying vec3 varying_pixel_position;
        uniform samplerCube uniform_cubemap;
        void main()
        {
            vec3 dir = normalize(varying_pixel_position);
            vec3 sample = texture(uniform_cubemap, vec3(-1.0, 1.0, 1.0) * dir);
            gl_FragColor = argb(sample);
        }
        </script>
    <script id="3d-vertex-shader" type="x-shader/x-vertex">
        attribute vec4 a_position;
        attribute vec2 a_texcoord;
        
        uniform mat4 u_matrix;
        
        varying vec2 v_texcoord;
        
        void main() {
          // Multiply the position by the matrix.
          gl_Position = u_matrix * a_position;
        
          // Pass the texcoord to the fragment shader.
          v_texcoord = a_texcoord;
        }
        </script>
        <!-- fragment shader -->
        <script id="3d-fragment-shader" type="x-shader/x-fragment">
        precision mediump float;
        
        // Passed in from the vertex shader.
        varying vec2 v_texcoord;
        
        // The texture.
        uniform sampler2D u_texture;
        
        void main() {
           gl_FragColor = texture2D(u_texture, v_texcoord);
        }
        </script>
             

    <script id="vFundShader" type="x-shader/x-vertex">#version 300 es
// an attribute is an input (in) to a vertex shader.
// It will receive data from a buffer
in vec4 a_position;
in vec2 a_texcoord;

// A matrix to transform the positions by
uniform mat4 u_matrix;

// a varying to pass the texture coordinates to the fragment shader
out vec2 v_texcoord;

// all shaders have a main function
void main() {
  // Multiply the position by the matrix.
  gl_Position = u_matrix * a_position;

  // Pass the texcoord to the fragment shader.
  v_texcoord = a_texcoord;
}
    </script>
    <script id="fFundShader" type="x-shader/x-vertex">#version 300 es

precision mediump float;

// Passed in from the vertex shader.
in vec2 v_texcoord;

// The texture.
uniform sampler2D u_texture;

// we need to declare an output for the fragment shader
out vec4 outColor;

void main() {
  outColor = texture(u_texture, v_texcoord);
}
    </script>
    <script id="vertexSquareShader" type="x-shader/x-vertex">
        attribute vec4 a_position;
        varying vec2 v_texcoord;
        uniform mat4 uModelViewMatrix;
        void main() {
          gl_Position = /*uModelViewMatrix **/ a_position;

         //gl_Position = vec4(a_position, 0, 1);
          // assuming a unit quad for position we
          // can just use that for texcoords. Flip Y though so we get the top at 0
          v_texcoord = a_position.xy * vec2(0.5, -0.5) + 0.5;
        }    
    </script>

    <script id="fragSquareShader" type="x-shader/x-vertex">
        precision mediump float;
        varying vec2 v_texcoord;
        uniform sampler2D u_image;
        uniform sampler2D u_palette;
            
        void main() {
         //   gl_FragColor = vec4(0,1,0,1);
            float index = texture2D(u_image, v_texcoord).a * 255.0;
           // vec4 textureColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
            gl_FragColor = texture2D(u_palette, vec2((index + 0.5) / 256.0, 0.5));
        }
    </script>
    
    <script id="vertexCubeLightShader" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec3 position;
    attribute vec3 normal;
    
    uniform float t;
    
    varying vec3 light;
    varying vec3 coord;
    
    void main() {
      light = normal;
      float c = cos(t);
      float s = sin(t);
      coord = position;
      vec3 lcoord = vec3(
        c*position.x-s*position.z,
        position.y,
        c*position.z+s*position.x+0.5);
      gl_Position = vec4(lcoord,1);
    }
</script>
    
<script id="fragCubeLightShader" type="x-shader/x-vertex">
    precision mediump float;

    varying vec3 light;
    varying vec3 coord;

    void main() {
    float x = max(light.x,0.0);
    float y = max(-light.x,0.0);
    if(gl_FrontFacing) {
        gl_FragColor = vec4(0.6+x+max(-light.y,0.0),x,-2.0*coord.z+x,1);
    } else {
        gl_FragColor = vec4(y+max(light.y,0.0),0.6+y+0.2*max(light.y,0.0),-2.0*coord.z+y,1);
  }
}
</script>
    

    <script id="vertexCubeShader" type="x-shader/x-vertex">
        //precision lowp float;
        attribute vec3 aVertexNormal;
        attribute vec2 a_texcoord;
        // a varying to pass the texture coordinates to the fragment shader
        varying vec2 v_texcoord;
        attribute vec4 aVertexPosition;
        attribute vec4 aVertexColor;

        uniform mat4 uNormalMatrix;
        uniform mat4 uModelViewMatrix;
        uniform mat4 uProjectionMatrix;
        varying lowp vec4 vColor;
        varying vec3 v_normal;
        
    void main(void) {
      gl_Position = /*uProjectionMatrix **/ uModelViewMatrix * aVertexPosition;
      vColor = aVertexPosition;
      v_texcoord = a_texcoord;
        v_normal = aVertexNormal;
      //highp vec4 transformedNormal =uNormalMatrix * vec4(aVertexNormal, 1.0);
    }
    </script>

    <script id="fragCubeShader" type="x-shader/x-vertex">
        precision mediump float; //new
        
        varying vec3 v_normal;
        uniform vec3 u_reverseLightDirection;
        uniform vec4 u_color;

        uniform sampler2D u_image;
        uniform sampler2D u_palette;
       
        varying vec2 v_texcoord;
        // The texture.
        //uniform sampler2D u_texture;

        varying lowp vec4 vColor;
        void main(void) {
         //gl_FragColor = texture2D(u_texture, v_texcoord);//vColor;

        // float index = texture2D(u_image, v_texcoord).a * 255.0;
            //vec4 textureColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
           //vec4 textureColor = texture2D(u_palette, vec2((index + 0.5) / 256.0, 0.5));


         gl_FragColor = vColor;
        
         vec3 normal = normalize(v_normal);
         float light =  max(dot(normal, u_reverseLightDirection),0.0);
         //gl_FragColor = u_color;
         gl_FragColor.rgb *= light;
    
         //http://learningwebgl.com/blog/?p=684
        // vec3 transformedNormal = uNMatrix * aVertexNormal;
        // float directionalLightWeighting = max(dot(transformedNormal, uLightingDirection), 0.0);
        // vLightWeighting = uAmbientColor + uDirectionalColor * directionalLightWeighting;
    }

    

    </script>
    <script id="per-fragment-lighting-fs" type="x-shader/x-fragment">
        precision mediump float;
        varying vec2 vTextureCoord;
        varying vec3 vTransformedNormal;
        varying vec4 vPosition;
        uniform vec3 uMaterialAmbientColor;
        uniform vec3 uMaterialDiffuseColor;
        uniform vec3 uMaterialSpecularColor;
        uniform float uMaterialShininess;
        uniform vec3 uMaterialEmissiveColor;
        uniform bool uShowSpecularHighlights;
        uniform bool uUseTextures;
        uniform vec3 uAmbientLightingColor;
        uniform vec3 uPointLightingLocation;
        uniform vec3 uPointLightingDiffuseColor;
        uniform vec3 uPointLightingSpecularColor;
        uniform sampler2D uSampler;

        varying lowp vec4 vColor;
        void main(void) {
            vec3 ambientLightWeighting = uAmbientLightingColor;
            vec3 lightDirection = normalize(uPointLightingLocation - vPosition.xyz);
            vec3 normal = normalize(vTransformedNormal);
            vec3 specularLightWeighting = vec3(0.0, 0.0, 0.0);
            if (uShowSpecularHighlights) {
                vec3 eyeDirection = normalize(-vPosition.xyz);
                vec3 reflectionDirection = reflect(-lightDirection, normal);
                float specularLightBrightness = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uMaterialShininess);
                specularLightWeighting = uPointLightingSpecularColor * specularLightBrightness;
            }
            float diffuseLightBrightness = max(dot(normal, lightDirection), 0.0);
            vec3 diffuseLightWeighting = uPointLightingDiffuseColor * diffuseLightBrightness;
            vec3 materialAmbientColor = uMaterialAmbientColor;
            vec3 materialDiffuseColor = uMaterialDiffuseColor;
            vec3 materialSpecularColor = uMaterialSpecularColor;
            vec3 materialEmissiveColor = uMaterialEmissiveColor;
            float alpha = 1.0;
            if (uUseTextures) {
                vec4 textureColor = texture2D(uSampler, vec2(vTextureCoord.s, vTextureCoord.t));
                materialAmbientColor = materialAmbientColor * textureColor.rgb;
                materialDiffuseColor = materialDiffuseColor * textureColor.rgb;
                materialEmissiveColor = materialEmissiveColor * textureColor.rgb;
                alpha = textureColor.a;
            }
            gl_FragColor = vec4(
                materialAmbientColor * ambientLightWeighting
                + materialDiffuseColor * diffuseLightWeighting
                + materialSpecularColor * specularLightWeighting
                + materialEmissiveColor,
                alpha
            );
           //gl_FragColor = vColor;
        }
    </script>
    
    <script id="per-fragment-lighting-vs" type="x-shader/x-vertex">
        attribute vec3 aVertexPosition;

       // attribute vec4 aVertexPosition;
        attribute vec4 aVertexColor;
        uniform mat4 uModelViewMatrix;
        varying lowp vec4 vColor;

        attribute vec3 aVertexNormal;
        attribute vec2 aTextureCoord;
        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;
        uniform mat3 uNMatrix;
        varying vec2 vTextureCoord;
        varying vec3 vTransformedNormal;
        varying vec4 vPosition;
        void main(void) {

          //  gl_Position = /*uProjectionMatrix **/ uModelViewMatrix * aVertexPosition;
           // vColor = aVertexPosition;
           

            vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
            gl_Position = uPMatrix * vPosition;
            vTextureCoord = aTextureCoord;
            vTransformedNormal = uNMatrix * aVertexNormal;
        }
    </script>

    <script id="vertexShader" type="x-shader/x-vertex">#version 300 es
        in vec4 a_position;
        in vec2 a_texcoord;
        uniform mat4 u_matrix;
        out vec2 v_texcoord;
        void main() {
            // Multiply the position by the matrix.
            gl_Position = u_matrix * a_position;
            // Pass the texcoord to the fragment shader.
            v_texcoord = a_texcoord;
        }
    </script>
    <script id="fragShader" type="x-shader/x-vertex">#version 300 es
        precision mediump float;
        // Passed in from the vertex shader.
        in vec2 v_texcoord;
        // The texture.
        uniform sampler2D u_texture;
        out vec4 myOutputColor;
    
        void main() 
        {
            myOutputColor = texture(u_texture, v_texcoord);
        }
    </script>
   
    <script id="vs" type="x-shader/x-vertex">
        #version 300 es
        #define POSITION_LOCATION 0
        #define TEXCOORD_LOCATION 4
        
        precision highp float;
        precision highp int;
        uniform mat4 MVP;
        layout(location = POSITION_LOCATION) in vec2 position;
        layout(location = TEXCOORD_LOCATION) in vec2 texcoord;
        out vec2 v_st;
        void main()
        {
            v_st = texcoord;
            gl_Position = MVP * vec4(position, 0.0, 1.0);
        }
    </script>

    <script id="fs" type="x-shader/x-fragment">
        #version 300 es
        precision highp float;
        precision highp int;
        precision highp sampler2DArray;
        uniform sampler2DArray diffuse;
        uniform int layer;
        in vec2 v_st;
        out vec4 color;
        void main()
        {
            color = texture(diffuse, vec3(v_st, layer));
        }
    </script>

    <script id="vsTest" type="x-shader/x-vertex">
        #version 300 es
        #define POSITION_LOCATION 0
        #define TEXCOORD_LOCATION 4
        
        precision highp float;
        precision highp int;
        uniform mat4 MVP;
        layout(location = POSITION_LOCATION) in vec2 position;
        layout(location = TEXCOORD_LOCATION) in vec2 texcoord;
        out vec2 v_st;
        void main()
        {
            v_st = texcoord;
            gl_Position = MVP * vec4(position, 0.0, 1.0);
        }
    </script>

    <script id="fsTest" type="x-shader/x-fragment">
        #version 300 es
        precision highp float;
        precision highp int;
        uniform sampler2D diffuse;
        in vec2 v_st;
        out vec4 color;
        void main()
        {
            color = texture(diffuse, v_st);
        }
    </script>

<script id="colorAdjustFragmentShader" type="text/something-not-javascript">
  precision mediump float;
  uniform float mixAmount;
  uniform sampler2D inTexture;
  uniform sampler2D colorCube0;
  uniform sampler2D colorCube1;
  varying vec2 v_texCoord;
  vec4 sampleAs3DTexture(sampler2D tex, vec3 texCoord, float size) {
     float sliceSize = 1.0 / size;                         // space of 1 slice
     float slicePixelSize = sliceSize / size;              // space of 1 pixel
     float sliceInnerSize = slicePixelSize * (size - 1.0); // space of size pixels
     float zSlice0 = min(floor(texCoord.z * size), size - 1.0);
     float zSlice1 = min(zSlice0 + 1.0, size - 1.0);
     float xOffset = slicePixelSize * 0.5 + texCoord.x * sliceInnerSize;
     float s0 = xOffset + (zSlice0 * sliceSize);
     float s1 = xOffset + (zSlice1 * sliceSize);
     vec4 slice0Color = texture2D(tex, vec2(s0, texCoord.y));
     vec4 slice1Color = texture2D(tex, vec2(s1, texCoord.y));
     float zOffset = mod(texCoord.z * size, 1.0);
     #if defined(USE_NEAREST)
       return mix(slice0Color, slice1Color, step(0.5, zOffset));
     #else
       return mix(slice0Color, slice1Color, zOffset);
     #endif
  }
  void main() {
    vec4 originalColor = texture2D(inTexture, v_texCoord);
    vec4 color0 = sampleAs3DTexture(colorCube0, originalColor.rgb, 8.0);
    vec4 color1 = sampleAs3DTexture(colorCube1, originalColor.rgb, 8.0);
    gl_FragColor = vec4(mix(color0, color1, mixAmount).rgb, originalColor.a);
  }
  </script>
</html>
